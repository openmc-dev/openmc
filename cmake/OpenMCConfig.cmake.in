@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/OpenMCConfigVersion.cmake")
include(CMakeFindDependencyMacro)

if (@OPENMC_USES_VENDORED_FMT@)
  find_dependency(fmt CONFIG REQUIRED HINTS "${PACKAGE_PREFIX_DIR}")
else()
  find_dependency(fmt CONFIG REQUIRED HINTS "@OPENMC_FMT_DIR@")
endif()
if (@OPENMC_USES_VENDORED_PUGIXML@)
  find_dependency(pugixml CONFIG REQUIRED HINTS "${PACKAGE_PREFIX_DIR}")
else()
  find_dependency(pugixml CONFIG REQUIRED HINTS "@OPENMC_PUGIXML_DIR@")
endif()
if (@OPENMC_USES_VENDORED_XTENSOR@)
  find_dependency(xtl CONFIG REQUIRED HINTS "${PACKAGE_PREFIX_DIR}")
else()
  find_dependency(xtl CONFIG REQUIRED HINTS "@OPENMC_XTL_DIR@")
endif()
if (@OPENMC_USES_VENDORED_XTENSOR@)
  find_dependency(xtensor CONFIG REQUIRED HINTS "${PACKAGE_PREFIX_DIR}")
else()
  find_dependency(xtensor CONFIG REQUIRED HINTS "@OPENMC_XTENSOR_DIR@")
endif()
if(@OPENMC_USE_DAGMC@)
  find_dependency(DAGMC REQUIRED HINTS @OPENMC_DAGMC_DIR@)
endif()

if(@OPENMC_USE_LIBMESH@)
  include(FindPkgConfig)
  list(APPEND CMAKE_PREFIX_PATH @LIBMESH_PREFIX@)
  set(PKG_CONFIG_USE_CMAKE_PREFIX_PATH True)
  pkg_check_modules(LIBMESH REQUIRED @LIBMESH_PC_FILE@>=1.7.0 IMPORTED_TARGET)
endif()

if(@OPENMC_USE_PNG@)
  find_dependency(PNG)
endif()

if(@OPENMC_USE_MPI@)
  find_dependency(MPI REQUIRED)
endif()

if(@OPENMC_USE_OPENMP@)
  find_dependency(OpenMP REQUIRED)
endif()

if(@OPENMC_USE_UWUW@ AND NOT ${DAGMC_BUILD_UWUW})
  message(FATAL_ERROR "UWUW is enabled in OpenMC but the DAGMC installation discovered was not configured with UWUW.")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/OpenMCTargets.cmake")

if(NOT OpenMC_FIND_QUIETLY)
    message(STATUS "Found OpenMC: ${PACKAGE_VERSION} (found in ${PACKAGE_PREFIX_DIR})")
endif()