@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/OpenMCConfigVersion.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/OpenMCTargets.cmake")

include(CMakeFindDependencyMacro)

get_filename_component(_OPENMC_PREFIX "${CMAKE_CURRENT_LIST_DIR}/../../.." ABSOLUTE)
find_dependency(fmt CONFIG REQUIRED HINTS ${_OPENMC_PREFIX})
find_dependency(pugixml CONFIG REQUIRED HINTS ${_OPENMC_PREFIX})
find_dependency(xtl CONFIG REQUIRED HINTS ${_OPENMC_PREFIX})
find_dependency(xtensor CONFIG REQUIRED HINTS ${_OPENMC_PREFIX})
if(@OPENMC_USE_DAGMC@)
  find_dependency(DAGMC REQUIRED HINTS @DAGMC_DIR@)
endif()

if(@OPENMC_USE_LIBMESH@)
  include(FindPkgConfig)
  list(APPEND CMAKE_PREFIX_PATH @LIBMESH_PREFIX@)
  set(PKG_CONFIG_USE_CMAKE_PREFIX_PATH True)
  pkg_check_modules(LIBMESH REQUIRED @LIBMESH_PC_FILE@>=1.7.0 IMPORTED_TARGET)
endif()

find_dependency(PNG)

if(@OPENMC_USE_MPI@)
  find_dependency(MPI REQUIRED)
endif()

if(@OPENMC_USE_OPENMP@)
  find_dependency(OpenMP REQUIRED)
endif()

if(@OPENMC_USE_UWUW@ AND NOT ${DAGMC_BUILD_UWUW})
  message(FATAL_ERROR "UWUW is enabled in OpenMC but the DAGMC installation discovered was not configured with UWUW.")
endif()

if(NOT OpenMC_FIND_QUIETLY)
    message(STATUS "Found OpenMC: ${PACKAGE_VERSION} (found in ${_OPENMC_PREFIX})")
endif()